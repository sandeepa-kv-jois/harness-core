Var one = 1
Var nullObj = null
Var dataCollectionURL = concat(Env:baseUrl, Env:urlPath)
Var logList = []
Var requestBody = concat("",Env:body)

if Env:startTimeFormat == "MILLISECONDS" {
   Var startTimeStr = concat("", Env:startTimeMillis)
   Var:dataCollectionURL.replaceAll(Env:startTimePlaceholder, Var:startTimeStr)
   Var:requestBody.replaceAll(Env:startTimePlaceholder, Var:startTimeStr)

}

if Env:startTimeFormat == "SECONDS" {
   Var startTimeStr = concat("", Env:startTimeSeconds)
   Var:dataCollectionURL.replaceAll(Env:startTimePlaceholder, Var:startTimeStr)
   Var:requestBody.replaceAll(Env:startTimePlaceholder, Var:startTimeStr)
}

if Env:endTimeFormat == "MILLISECONDS" {
   Var endTimeStr = concat("", Env:endTimeMillis)
   Var:dataCollectionURL.replaceAll(Env:endTimePlaceholder, Var:endTimeStr)
   Var:requestBody.replaceAll(Env:endTimePlaceholder, Var:endTimeStr)
}

if Env:endTimeFormat == "SECONDS" {
   Var endTimeStr = concat("", Env:endTimeSeconds)
   Var:dataCollectionURL.replaceAll(Env:endTimePlaceholder, Var:endTimeStr)
   Var:requestBody.replaceAll(Env:endTimePlaceholder, Var:endTimeStr)
}

Var jsonBody = Var:requestBody.jsonify()

Request request = new Request(url=Var:dataCollectionURL, header=Env:commonHeaders, options=Env:commonOptions, body=Var:jsonBody)
print Var:request
Var response = sync-call(Var:request)
print Var:response

if Var:response == Var:nullObj {
   return Var:logList
}

Var queryValArr = extract(Var:response,Env:logMessageJSONPath)
Var timestampArr = extract(Var:response,Env:timestampValueJSONPath)
Var serviceInstanceArr = extract(Var:response,Env:serviceInstanceJSONPath)
Var serviceInstance = null

if Var:queryValArr != Var:nullObj  {

   Var totalQueryResults = Var:queryValArr.length()
   Var innerIndex = 0

   while Var:innerIndex != Var:totalQueryResults {
      Var queryVal = Var:queryValArr[Var:innerIndex]
      Var timestamp = Var:timestampArr[Var:innerIndex]
      Var timeStampVal = string-to-time(Var:timestamp)
      Var host = Var:serviceInstanceArr[Var:innerIndex]

      if Var:queryVal != Var:nullObj {
         Var logRecord = new LogDataRecord(log=Var:queryVal, timestamp=Var:timeStampVal, host=Var:host)
         Var:logList.append(Var:logRecord)
      }
      innerIndex = Var:innerIndex + Var:one
   }
}

return Var:logList