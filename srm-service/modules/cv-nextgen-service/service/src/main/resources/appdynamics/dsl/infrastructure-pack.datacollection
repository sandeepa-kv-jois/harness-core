Var auth
if Env:tokenBasedAuth == "true" {
  Var authUrlSuffix = "api/oauth/access_token"
  Var authUrl = concat(Env:baseUrl,Var:authUrlSuffix)
  Var bodyMap = {}
  Var:bodyMap.put("grant_type", "client_credentials")
  Var:bodyMap.put("client_id", Env:clientId)
  Var:bodyMap.put("client_secret", Env:clientSecret)
  Var authResponse = sync-call(url=Var:authUrl, header=Env:commonHeaders, body=Var:bodyMap, bodyType=FORM_URL_ENCODED)
  Var token = extract(Var:authResponse,$.access_token)
  Var bearerStr = concat("Bearer ", Var:token)
  Env:commonHeaders.put("Authorization", Var:bearerStr)

}

Var tierDetailsUrl = concat(Env:baseUrl, "rest/applications/", Env:appId, "/tiers/", Env:tierId, "?output=json")
Var tierDetail = sync-call(url=Var:tierDetailsUrl, header=Env:commonHeaders)
Var tierName = "test"
foreach tier in Var:tierDetail {
  tierName = extract(Var:tier,$.name)
}

Var metricDataUrl = concat(Env:baseUrl, "rest/applications/", Env:appId, "/metric-data?output=JSON&time-range-type=BETWEEN_TIMES&rollup=false&start-time=", Env:startTimeMillis, "&end-time=", Env:endTimeMillis, "&metric-path=")
Var metricRequests = []
foreach metricDefinition in Env:metricsToCollect {
  Var btName = extract(Var:bt,$.name)
  Var metricPath = concat("", Var:metricDefinition)
  Var:metricPath.replaceAll("__tier_name__", Var:tierName)
  Var metricCall = concat(Var:metricDataUrl, Var:metricPath)
  Request metricRequest = new Request(url=Var:metricCall, header=Env:commonHeaders)
  Var:metricRequests.append(Var:metricRequest)
}
Var metrics = execute-parallel(Var:metricRequests)

Var metricsToReturn = []

Var index = 0
Var one = 1
foreach metricArr in Var:metrics {
  Var metricIdentifier =  Env:metricIdentifiers[Var:index]
  foreach metric in Var:metricArr {
    Var metricName = extract(Var:metric,$.metricName)
    if Var:metricName != "METRIC DATA NOT FOUND" {
      Var metricPath = extract(Var:metric,$.metricPath)
      Var metricPathSplit = Var:metricPath.split("|")
      Var txnName = Var:metricPathSplit[3]
      Var mName = Var:metricPathSplit[4]

      Var metricValues = extract(Var:metric,$.metricValues)
      foreach metricValue in Var:metricValues {
        Var value = extract(Var:metricValue,$.value)
        Var timestamp = extract(Var:metricValue,$.startTimeInMillis)
        Var tsRecord = new TimeseriesRecord(txnName=Var:txnName, metricIdentifier=Var:metricIdentifier, metricName=Var:mName, metricValue=Var:value, timestamp=Var:timestamp)
        Var:metricsToReturn.append(Var:tsRecord)
      }
    }
  }
  index = Var:index + Var:one
}
return Var:metricsToReturn
